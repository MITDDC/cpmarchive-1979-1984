;
;				FORTH.MAC
;
;
;Files FORT1.Z80, FORT2.Z80, FORT3.Z80, and FORT4.Z80 from
;Mark Zachmann were combined. The resulting file was set up
;for Microsoft's M80 as follows:
;
;	1. .Z80 and ASEG added,
;	2. Colons deleted from end of constants' names in EQU
;		statements,
;	3. Colons added to the end of several labels,
;	4. DM changed to DC,
;	5. Six statements containing AND, OR, or XOR corrected,
;	6. INITDP: DS    EM-$  changed to  INITDP  EQU   EM-$
;		at end of program.
;
;P. L. Kelley (11/06/81)
;
;
;The following are Mark Zachmann's comments.
;
;	This is for the most part FIG-FORTH version 1.1
;	modified for a Z-80 assembler, and containing some
;	Z-80 only instructions. Primary changes from standard
;	are sped-up CMOVE and FILL , and re-entrant port I/O.
;		CAUTION  the port I/O uses alternate registers
;
;	There are a few bug fixes, all relatively minor. For example
;	 LIST now puts line numbers in decimal, but reverts to the 
;	 selected base (my copy left it in decimal).
;
;	To configure, set ORG to lowest memory address FORTH can use
;	set EM to highest available (leaving BIOS, at least)+1
;	Set NSCR to the number of screen buffers desired
;		must have NBUF>1
;
;
; OFFSET is set to cold start at 250 to allow for a FORTH.COM
;  	file and, if needed, a CDOS.COM file. Look in the COLD 
; 	START section.
;
; The source code is in files FORT1.Z80, FORT2.Z80, FORT3.Z80
;		AND FORT4.Z80. The file FORTH.OBJ contains object code
;		for the setup given (62K CP/M). The file FORTXRF.TXT
;		is a crossreference of the assembly.
;
;	For more information contact Mark Zachmann 
;	 after MAY 1 (there will be an address here then)
;	Or much better yet, contact the FIG-FORTH group.
;
; This is guaranteed to assemble on a CROMEMCO assembler.!
;
;	4/6/81
;
; *************************************************
;
	.Z80
;
FIGREL	EQU	1	; FIG release #
FIGREV	EQU	1	; FIG revision #
USRVER	EQU	2	; second version (MSZ)
;
ABL	EQU	20H	; BLank
ACR	EQU	0DH	; Carriage return
ADOT	EQU	02EH
BELL	EQU	07H	; ASCII bell
BSIN	EQU	08H	; Backspace as rubout .. my preference
BSOUT	EQU	08H	; Backspace output
DLE	EQU	10H	; Printer toggle (^P)
LF	EQU	0AH	; LF
FF	EQU	0CH	; Form Feed
;
EM	EQU	0B000H	; The top of memory+1
;			   set to just below IOS
NSCR	EQU	4	; Number of 1024 byte screen buffers
KBBUF	EQU	128	; Data bytes per disk buffer for VS
US	EQU	40H	; User variable space
RTS	EQU	0A0H	; return stack and term buffer space
;
CO	EQU	KBBUF+4	; disk buffer + 2 hdr + 2 tail
NBUF	EQU	NSCR*400H/KBBUF	; number of buffers
LENBUF	EQU	CO*NBUF		; total length of buffers
BUF1	EQU	EM-LENBUF	; ADDR of first disk buffer
INITR0	EQU	BUF1-US
INITS0	EQU	INITR0-RTS
;
;
	ASEG
;
	ORG	100H
ORIG:	NOP
	JP	CLD	; Cold start setup
	NOP
	JP	WRM	; Warm start setup
	DB	FIGREL
	DB	FIGREV
	DB	USRVER
	DB	0EH	; Implemetation attributes (see FIG man)
	DW	TASK-7	; Topmost word in Forth Vocabulary
	DW	BSIN	; backspace char
	DW	INITR0  ; INIT (UP)
;	THE FOLLOWING IS USED IN COLD START AND
;	MAY BE CHANGED BUT NOT REARRANGED
;
;
	DW	INITS0
	DW	INITR0
	DW	INITS0
	DW	1FH	; INIT (Width)
	DW	0	; INIT (Warning)
	DW	INITDP	; INIT (Fence)
	DW	INITDP	; INIT (DP)
	DW	FORTH+8	; INIT (Voc-Link)
	DW	5H,0B320H	; not used.. normally CPU name base 36
;
;
;
;	FORTH REGISTERS
;
;	IP	--	BC	Should be preserved across FORTH words
;	W	--	DE	Sometimes output from NEXT
;				 may be altered before jumping to NEXT
;				 input only when DPUSH called
;	SP	--	SP	Should be used only as data stack
;				 accross FORTH words, may be used
;				 within words if restored before NEXT
;
;	***********************************************
;
;	DEBUG support
;		to use : set BIP to IP value to halt at (not CFA)
;		 set a Breakpoint (DDT?) at BREAK or patch HLT there
;		 patch a JP TNEXT at NEXT
;		when (IP)=(BIP) cpu will halt
;
;	***********************************************
;
UP:	DW	INITR0
RPP:	DW	INITR0
;
BIP:	DW	0
;
TNEXT:	LD	HL,BIP
	LD	A,(HL)
	CP	C
	JP	NZ,TNEXT1
	INC	HL
	LD	A,(HL)
	CP	B
	JP	NZ,TNEXT1
BREAK:	NOP
	NOP
	NOP
TNEXT1:	LD	A,(BC)
	INC	BC
	LD	L,A
	JP	NEXT+3
;
;	***********************************
;
;	NEXT, THE FORTH ADDRESS INTERPRETER
;
;	***********************************
;
DPUSH:	PUSH	DE
HPUSH:	PUSH	HL
NEXT:	LD	A,(BC)	; (W) <- ((IP))
	INC	BC	; (IP) <- (IP)+2
	LD	L,A
	LD	A,(BC)
	INC	BC
	LD	H,A	; (HL) <- CFA
NEXT1:	LD	E,(HL)	;(PC) <- ((W))
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	JP	(HL)	; note: (DE) = CFA+1
;
;
;*********************************************************
;
;	FORTH DICTIONARY
;
;*********************************************************
;
;
;	Dictionary Format:
;
;	Address Name		Byte Contents
;
;  ( MSB=1 P=precedence bit  S=smudge bit )
;	NFA	name field	1PS<len>    -- name length
;				0<1char>
;				0<2char>
;				  ..... etc
;
;
;	LFA	link field	<linklowbyte>	= previous words NFA
;				<linkhighbyte>
;
;LABEL:	CFA	code field	<codelowbyte>	= address of CPU code
;				<codehighbyte>
;
;	PFA	parameter fld	<1param>	-- 1st parameter byte
;				<2param>
;				  ...... etc
;
;*******************************************************
;
;
;
DP0:	DB	083H	;LIT
	DC	'LIT'
	DW	0	;LFA=0 END OF DICT.
LIT:	DW	$+2
	LD	A,(BC)
	INC	BC
	LD	L,A
	LD	A,(BC)
	INC	BC
	LD	H,A
	JP	HPUSH
;
	DB	87H	;EXECUTE
	DC	'EXECUTE'
	DW	LIT-6
EXEC:	DW	$+2
	POP	HL
	JP	NEXT1
;
	DB	86H
	DC	'BRANCH'
	DW	EXEC-0AH
BRAN:	DW	$+2
BRAN1:	LD	H,B
	LD	L,C
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	DEC	HL
	ADD	HL,DE
	LD	C,L
	LD	B,H
	JP	NEXT
;
	DB	87H	;0BRANCH
	DC	'0BRANCH'
	DW	BRAN-9
ZBRAN:	DW	$+2
	POP	HL
	LD	A,L
	OR	H
	JP	Z,BRAN1
	INC	BC
	INC	BC
	JP	NEXT
;
	DB	86H	;(LOOP)
	DC	'(LOOP)'
	DW	ZBRAN-0AH
XLOOP:	DW	$+2
	LD	DE,1
XLOO1:	LD	HL,(RPP)
	LD	A,(HL)
	ADD	A,E
	LD	(HL),A
	LD	E,A
	INC	HL
	LD	A,(HL)
	ADC	A,D
	LD	(HL),A
	INC	HL
	INC	D
	DEC	D
	LD	D,A
	JP	M,XLOO2
	LD	A,E
	SUB	(HL)
	LD	A,D
	INC	HL
	SBC	A,(HL)
	JP	XLOO3
XLOO2:	LD	A,(HL)
	SUB	E
	INC	HL
	LD	A,(HL)
	SBC	A,D
XLOO3:	JP	M,BRAN1
	INC	HL
	LD	(RPP),HL
	INC	BC
	INC	BC
	JP	NEXT
;
	DB	87H
	DC	'(+LOOP)'
	DW	XLOOP-9
XPLOO:	DW	$+2
	POP	DE
	JP	XLOO1
;
	DB	84H
	DC	'(DO)'
	DW	XPLOO-0AH
XDO:	DW	$+2
	LD	HL,(RPP)
	DEC	HL
	DEC	HL
	DEC	HL
	DEC	HL
	LD	(RPP),HL
	POP	DE
	LD	(HL),E
	INC	HL
	LD	(HL),D
	POP	DE
	INC	HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	JP	NEXT
;
	DB	81H
	DC	'I'
	DW	XDO-7
IDO:	DW	$+2
	LD	HL,(RPP)
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	PUSH	DE
	JP	NEXT
;
	DB	85H
	DC	'DIGIT'
	DW	IDO-4
DIGIT:	DW	$+2
	POP	HL
	POP	DE
	LD	A,E
	SUB	30H
	JP	M,DIGI2
	CP	0AH
	JP	M,DIGI1
	SUB	07H
	CP	0AH
	JP	M,DIGI2
DIGI1:	CP	L
	JP	P,DIGI2
	LD	E,A
	LD	HL,1
	JP	DPUSH
DIGI2:	LD	L,H
	JP	HPUSH
;
	DB	86H
	DC	'(FIND)'
	DW	DIGIT-8
PFIND:	DW	$+2
	POP	DE
PFIN1:	POP	HL
	PUSH	HL
	LD	A,(DE)
	XOR	(HL)
	AND	3FH
	JP	NZ,PFIN4
PFIN2:	INC	HL
	INC	DE
	LD	A,(DE)
	XOR	(HL)
	ADD	A,A
	JP	NZ,PFIN3
	JP	NC,PFIN2
	LD	HL,5
	ADD	HL,DE
	EX	(SP),HL
PFIN6:	DEC	DE
	LD	A,(DE)
	OR	A
	JP	P,PFIN6
	LD	E,A
	LD	D,0
	LD	HL,1
	JP	DPUSH
PFIN3:	JP	C,PFIN5
PFIN4:	INC	DE
	LD	A,(DE)
	OR	A
	JP	P,PFIN4
PFIN5:	INC	DE
	EX	DE,HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	LD	A,D
	OR	E
	JP	NZ,PFIN1
	POP	HL
	LD	HL,0
	JP	HPUSH
;
	DB	87H
	DC	'ENCLOSE'
	DW	PFIND-9
ENCL:	DW	$+2
	POP	DE
	POP	HL
	PUSH	HL
	LD	A,E
	LD	D,A
	LD	E,-1
	DEC	HL
ENCL1:	INC	HL
	INC	E
	CP	(HL)
	JP	Z,ENCL1
	LD	D,0
	PUSH	DE
	LD	D,A
	LD	A,(HL)
	AND	A
	JP	NZ,ENCL2
	LD	D,0
	INC	E
	PUSH	DE
	DEC	E
	PUSH	DE
	JP	NEXT
ENCL2:	LD	A,D
	INC	HL
	INC	E
	CP	(HL)
	JP	Z,ENCL4
	LD	A,(HL)
	AND	A
	JP	NZ,ENCL2
ENCL3:	LD	D,0
	PUSH	DE
	PUSH	DE
	JP	NEXT
ENCL4:	LD	D,0
	PUSH	DE
	INC	E
	PUSH	DE
	JP	NEXT
;
	DB	84H
	DC	'EMIT'
	DW	ENCL-0AH
EMIT:	DW	DOCOL
	DW	PEMIT
	DW	ONE,OUTT
	DW	PSTOR,SEMIS
;
	DB	83H
	DC	'KEY'
	DW	EMIT-7
KEY:	DW	$+2
	JP	PKEY
;
	DB	89H
	DC	'?TERMINAL'
	DW	KEY-6
QTERM:	DW	$+2
	JP	PQTER
;
	DB	82H
	DC	'CR'
	DW	QTERM-0CH
CR:	DW	$+2
	JP	PCR
;
	DB	85H
	DC	'CMOVE'
	DW	CR-5
CMOVE:	DW	$+2
	EXX
	POP	BC
	POP	DE
	POP	HL
	LD	A,B
	OR	C
	JR	Z,CMOV1
	LDIR
CMOV1:	EXX
	JP	NEXT
;
	DB	82H
	DC	'U*'
	DW	CMOVE-8
USTAR:	DW	$+2
	POP	DE
	POP	HL
	PUSH	BC
	LD	B,H
	LD	A,L
	CALL	MPYX
	PUSH	HL
	LD	H,A
	LD	A,B
	LD	B,H
	CALL	MPYX
	POP	DE
	LD	C,D
	ADD	HL,BC
	ADC	A,0
	LD	D,L
	LD	L,H
	LD	H,A
	POP	BC
	PUSH	DE
	JP	HPUSH
MPYX:	LD	HL,0
	LD	C,8
MPYX1:	ADD	HL,HL
	RLA
	JP	NC,MPYX2
	ADD	HL,DE
	ADC	A,0
MPYX2:	DEC	C
	JP	NZ,MPYX1
	RET
;
	DB	82H
	DC	'U/'
	DW	USTAR-5
USLAS:	DW	$+2
	LD	HL,4
	ADD	HL,SP
	LD	E,(HL)
	LD	(HL),C
	INC	HL
	LD	D,(HL)
	LD	(HL),B
	POP	BC
	POP	HL
	LD	A,L
	SUB	C
	LD	A,H
	SBC	A,B
	JP	C,USLA1
	LD	HL,0FFFFH
	LD	DE,0FFFFH
	JP	USLA7
USLA1:	LD	A,16
USLA2:	ADD	HL,HL
	RLA
	EX	DE,HL
	ADD	HL,HL
	JP	NC,USLA3
	INC	DE
	AND	A
USLA3:	EX	DE,HL
	RRA
	PUSH	AF
	JP	NC,USLA4
	LD	A,L
	SUB	C
	LD	L,A
	LD	A,H
	SBC	A,B
	LD	H,A
	JP	USLA5
USLA4:	LD	A,L
	SUB	C
	LD	L,A
	LD	A,H
	SBC	A,B
	LD	H,A
	JP	NC,USLA5
	ADD	HL,BC
	DEC	DE
USLA5:	INC	DE
USLA6:	POP	AF
	DEC	A
	JP	NZ,USLA2
USLA7:	POP	BC
	PUSH	HL
	PUSH	DE
	JP	NEXT
;
	DB	83H
	DC	'AND'
	DW	USLAS-5
ANDD:	DW	$+2
	POP	DE
	POP	HL
	LD	A,E
	AND	L
	LD	L,A
	LD	A,D
	AND	H
	LD	H,A
	JP	HPUSH
;
	DB	82H
	DC	'OR'
	DW	ANDD-6
ORR:	DW	$+2
	POP	DE
	POP	HL
	LD	A,E
	OR	L
	LD	L,A
	LD	A,D
	OR	H
	LD	H,A
	JP	HPUSH
;
	DB	83H
	DC	'XOR'
	DW	ORR-5
XORR:	DW	$+2
	POP	DE
	POP	HL
	LD	A,E
	XOR	L
	LD	L,A
	LD	A,D
	XOR	H
	LD	H,A
	JP	HPUSH
;
	DB	83H
	DC	'SP@'
	DW	XORR-6
SPAT:	DW	$+2
	LD	HL,0
	ADD	HL,SP
	JP	HPUSH
	DB	83H
	DC	'SP!'
	DW	SPAT-6
SPSTO:	DW	$+2
	LD	HL,(UP)
	LD	DE,6
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	LD	SP,HL
	JP	NEXT
;
	DB	83H
	DC	'RP@'
	DW	SPSTO-6
RPAT:	DW	$+2
	LD	HL,(RPP)
	JP	HPUSH
;
	DB	83H
	DC	'RP!'
	DW	RPAT-6
RPSTO:	DW	$+2
	LD	HL,(UP)
	LD	DE,8
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	EX	DE,HL
	LD	(RPP),HL
	JP	NEXT
;
	DB	82H
	DC	';S'
	DW	RPSTO-6
SEMIS:	DW	$+2
	LD	HL,(RPP)
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	LD	(RPP),HL
	JP	NEXT
;
	DB	85H
	DC	'LEAVE'
	DW	SEMIS-5
LEAVE:	DW	$+2
	LD	HL,(RPP)
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	JP	NEXT
;
	DB	82H
	DC	'>R'
	DW	LEAVE-8
TOR:	DW	$+2
	POP	DE
	LD	HL,(RPP)
	DEC	HL
	DEC	HL
	LD	(RPP),HL
	LD	(HL),E
	INC	HL
	LD	(HL),D
	JP	NEXT
;
	DB	82H
	DC	'R>'
	DW	TOR-5
FROMR:	DW	$+2
	LD	HL,(RPP)
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	INC	HL
	LD	(RPP),HL
	PUSH	DE
	JP	NEXT
;
	DB	81H
	DC	'R'
	DW	FROMR-5
RR:	DW	IDO+2	;!!
;
	DB	82H
	DC	'0='
	DW	RR-4
ZEQU:	DW	$+2
	POP	HL
	LD	A,L
	OR	H
	LD	HL,0
	JP	NZ,ZEQU1
	INC	HL
ZEQU1:	JP	HPUSH
;
	DB	82H
	DC	'0<'
	DW	ZEQU-5
ZLESS:	DW	$+2
	POP	HL
	ADD	HL,HL
	LD	HL,0
	JP	NC,ZLES1
	INC	HL
ZLES1:	JP	HPUSH
;
	DB	81H
	DC	'+'
	DW	ZLESS-5
PLUS:	DW	$+2
	POP	DE
	POP	HL
	ADD	HL,DE
	JP	HPUSH
;
	DB	82H
	DC	'D+'
	DW	PLUS-4
DPLUS:	DW	$+2
	LD	HL,6
	ADD	HL,SP
	LD	E,(HL)
	LD	(HL),C
	INC	HL
	LD	D,(HL)
	LD	(HL),B
	POP	BC
	POP	HL
	ADD	HL,DE
	EX	DE,HL
	POP	HL
	LD	A,L
	ADC	A,C
	LD	L,A
	LD	A,H
	ADC	A,B
	LD	H,A
	POP	BC
	PUSH	DE
	JP	HPUSH
;
	DB	85H
	DC	'MINUS'
	DW	DPLUS-5
MINUS:	DW	$+2
	POP	HL
	LD	A,L
	CPL
	LD	L,A
	LD	A,H
	CPL
	LD	H,A
	INC	HL
	JP	HPUSH
;
	DB	86H
	DC	'DMINUS'
	DW	MINUS-8
DMINU:	DW	$+2
	POP	HL
	POP	DE
	SUB	A
	SUB	E
	LD	E,A
	LD	A,0
	SBC	A,D
	LD	D,A
	LD	A,0
	SBC	A,L
	LD	L,A
	LD	A,0
	SBC	A,H
	LD	H,A
	PUSH	DE
	JP	HPUSH
;
	DB	84H
	DC	'OVER'
	DW	DMINU-9
OVER:	DW	$+2
	POP	DE
	POP	HL
	PUSH	HL
	JP	DPUSH
;
	DB	84H
	DC	'DROP'
	DW	OVER-7
DROP:	DW	$+2
	POP	HL
	JP	NEXT
;
	DB	84H
	DC	'SWAP'
	DW	DROP-7
SWAP:	DW	$+2
	POP	HL
	EX	(SP),HL
	JP	HPUSH
;
	DB	83H
	DC	'DUP'
	DW	SWAP-7
DUP:	DW	$+2
	POP	HL
	PUSH	HL
	JP	HPUSH
;
	DB	84H
	DC	'2DUP'
	DW	DUP-6
TDUP:	DW	$+2
	POP	HL
	POP	DE
	PUSH	DE
	PUSH	HL
	JP	DPUSH
;
	DB	82H
	DC	'+!'
	DW	TDUP-7
PSTOR:	DW	$+2
	POP	HL
	POP	DE
	LD	A,(HL)
	ADD	A,E
	LD	(HL),A
	INC	HL
	LD	A,(HL)
	ADC	A,D
	LD	(HL),A
	JP	NEXT
;
	DB	86H
	DC	'TOGGLE'
	DW	PSTOR-5
TOGGL:	DW	$+2
	POP	DE
	POP	HL
	LD	A,(HL)
	XOR	E
	LD	(HL),A
	JP	NEXT
;
	DB	81H
	DC	'@'
	DW	TOGGL-9
AT:	DW	$+2
	POP	HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	PUSH	DE
	JP	NEXT
;
	DB	82H
	DC	'C@'
	DW	AT-4
CAT:	DW	$+2
	POP	HL
	LD	L,(HL)
	LD	H,0
	JP	HPUSH
;
	DB	82H
	DC	'2@'
	DW	CAT-5
TAT:	DW	$+2
	POP	HL
	LD	DE,2
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	PUSH	DE
	LD	DE,-3
	ADD	HL,DE
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	PUSH	DE
	JP	NEXT
;
	DB	81H
	DC	'!'
	DW	TAT-5
STORE:	DW	$+2
	POP	HL
	POP	DE
	LD	(HL),E
	INC	HL
	LD	(HL),D
	JP	NEXT
;
	DB	82H
	DC	'C!'
	DW	STORE-4
CSTOR:	DW	$+2
	POP	HL
	POP	DE
	LD	(HL),E
	JP	NEXT
;
	DB	82H
	DC	'2!'
	DW	CSTOR-5
TSTOR:	DW	$+2
	POP	HL
	POP	DE
	LD	(HL),E
	INC	HL
	LD	(HL),D
	INC	HL
	POP	DE
	LD	(HL),E
	INC	HL
	LD	(HL),D
	JP	NEXT
;
	DB	0C1H
	DC	':'
	DW	TSTOR-5
COLON:	DW	DOCOL
	DW	QEXEC
	DW	SCSP
	DW	CURR
	DW	AT
	DW	CONT
	DW	STORE
	DW	CREAT
	DW	RBRAC
	DW	PSCOD
DOCOL:	LD	HL,(RPP)
	DEC	HL
	LD	(HL),B
	DEC	HL
	LD	(HL),C
	LD	(RPP),HL
	INC	DE
	LD	C,E
	LD	B,D
	JP	NEXT
	DB	0C1H
	DC	';'
	DW	COLON-4
SEMI:	DW	DOCOL
	DW	QCSP
	DW	COMP
	DW	SEMIS
	DW	SMUDG
	DW	LBRAC
	DW	SEMIS
;
	DB	84H
	DC	'NOOP'
	DW	SEMI-4
NOOP:	DW	DOCOL
	DW	SEMIS
;
	DB	88H
	DC	'CONSTANT'
	DW	NOOP-7
CON:	DW	DOCOL
	DW	CREAT
	DW	SMUDG
	DW	COMMA
	DW	PSCOD
DOCON:	INC	DE
	EX	DE,HL
	LD	E,(HL)
	INC	HL
	LD	D,(HL)
	PUSH	DE
	JP	NEXT
;
	DB	88H
	DC	'VARIABLE'
	DW	CON-0BH
VAR:	DW	DOCOL
	DW	CON
	DW	PSCOD
DOVAR:	INC	DE
	PUSH	DE
	JP	NEXT
;
	DB	84H
	DC	'USER'
	DW	VAR-0BH
USER:	DW	DOCOL
	DW	CON
	DW	PSCOD
DOUSE:	INC	DE
	EX	DE,HL
	LD	E,(HL)
	LD	D,0
	LD	HL,(UP)
	ADD	HL,DE
	JP	HPUSH
;
	DB	81H
	DC	'0'
	DW	USER-7
ZERO:	DW	DOCON
	DW	0
;
	DB	81H
	DC	'1'
	DW	ZERO-4
ONE:	DW	DOCON
	DW	1
;
	DB	81H
	DC	'2'
	DW	ONE-4
TWO:	DW	DOCON
	DW	2
;
	DB	81H
	DC	'3'
	DW	TWO-4
THREE:	DW	DOCON
	DW	3
;
	DB	82H
	DC	'BL'
	DW	THREE-4
BL:	DW	DOCON
	DW	20H
;
	DB	83H
	DC	'C/L'
	DW	BL-5
CSLL:	DW	DOCON
	DW	64
;
	DB	85H
	DC	'FIRST'
	DW	CSLL-6
FIRST:	DW	DOCON
	DW	BUF1
;
	DB	85H
	DC	'LIMIT'
	DW	FIRST-8
LIMIT:	DW	DOCON
	DW	EM
;
	DB	85H
	DC	'B/BUF'
	DW	LIMIT-8
BBUF:	DW	DOCON
	DW	KBBUF
;
	DB	85H
	DC	'B/SCR'
	DW	BBUF-8
BSCR:	DW	DOCON
	DW	400H/KBBUF
;
	DB	87H
	DC	'+ORIGIN'
	DW	BSCR-8
PORIG:	DW	DOCOL
	DW	LIT
	DW	ORIG
	DW	PLUS
	DW	SEMIS
;
;	USER VARIABLES
	DB	82H
	DC	'S0'
	DW	PORIG-0AH
SZERO:	DW	DOUSE
	DW	6
;
	DB	82H
	DC	'R0'
	DW	SZERO-5
RZERO:	DW	DOUSE
	DW	8
;
	DB	83H
	DC	'TIB'
	DW	RZERO-5
TIB:	DW	DOUSE
	DB	0AH
;
	DB	85H
	DC	'WIDTH'
	DW	TIB-6
WIDTH:	DW	DOUSE
	DB	0CH
;
	DB	87H
	DC	'WARNING'
	DW	WIDTH-8
WARN:	DW	DOUSE
	DB	0EH
;
	DB	85H
	DC	'FENCE'
	DW	WARN-0AH
FENCE:	DW	DOUSE
	DB	10H
;
	DB	82H
	DC	'DP'
	DW	FENCE-8
DP:	DW	DOUSE
	DB	12H
;
	DB	88H
	DC	'VOC-LINK'
	DW	DP-5
VOCL:	DW	DOUSE
	DW	14H
;
	DB	83H
	DC	'BLK'
	DW	VOCL-0BH
BLK:	DW	DOUSE
	DB	16H
	DB	82H
	DC	'IN'
	DW	BLK-6
INN:	DW	DOUSE
	DB	18H
;
	DB	83H
	DC	'OUT'
	DW	INN-5
OUTT:	DW	DOUSE
	DB	1AH
;
	DB	83H
	DC	'SCR'
	DW	OUTT-6
SCR:	DW	DOUSE
	DB	1CH
;
	DB	86H
	DC	'OFFSET'
	DW	SCR-6
OFSET:	DW	DOUSE
	DB	1EH
;
	DB	87H
	DC	'CONTEXT'
	DW	OFSET-9
CONT:	DW	DOUSE
	DB	20H
;
	DB	87H
	DC	'CURRENT'
	DW	CONT-0AH
CURR:	DW	DOUSE
	DB	22H
;
	DB	85H
	DC	'STATE'
	DW	CURR-0AH
STATE:	DW	DOUSE
	DB	24H
;
	DB	84H
	DC	'BASE'
	DW	STATE-8
BASE:	DW	DOUSE
	DB	26H
;
	DB	83H
	DC	'DPL'
	DW	BASE-7
DPL:	DW	DOUSE
	DB	28H
;
	DB	83H
	DC	'FLD'
	DW	DPL-6
FLD:	DW	DOUSE
	DB	2AH
;
	DB	83H
	DC	'CSP'
	DW	FLD-6
CSPP:	DW	DOUSE
	DB	2CH
;
	DB	82H
	DC	'R#'
	DW	CSPP-6
RNUM:	DW	DOUSE
	DB	2EH
;
	DB	83H
	DC	'HLD'
	DW	RNUM-5
HLD:	DW	DOUSE
	DW	30H
;
;	END OF USER VARIABLES
;
	DB	82H
	DC	'1+'
	DW	HLD-6
ONEP:	DW	$+2	; MODIFIED !
	POP	HL
	INC	HL
	JP	HPUSH
;
	DB	82H
	DC	'2+'
	DW	ONEP-5
TWOP:	DW	$+2	; MODIFIED
	POP	HL
	INC	HL
	INC	HL
	JP	HPUSH
;
	DB	84H
	DC	'HERE'
	DW	TWOP-5
HERE:	DW	DOCOL
	DW	DP
	DW	AT
	DW	SEMIS
;
	DB	85H
	DC	'ALLOT'
	DW	HERE-7
ALLOT:	DW	DOCOL
	DW	DP
	DW	PSTOR
	DW	SEMIS
;
	DB	81H
	DC	','
	DW	ALLOT-8
COMMA:	DW	DOCOL
	DW	HERE
	DW	STORE
	DW	TWO
	DW	ALLOT
	DW	SEMIS
;
	DB	82H
	DC	'C,'
	DW	COMMA-4
CCOM:	DW	DOCOL
	DW	HERE
	DW	CSTOR
	DW	ONE
	DW	ALLOT
	DW	SEMIS
;
;
	DB	81H
	DC	'-'
	DW	CCOM-5
SUBB:	DW	$+2
	POP	DE
	POP	HL
	OR	A
	SBC	HL,DE
	JP	HPUSH
;
	DB	81H
	DC	'='
	DW	SUBB-4
EQUAL:	DW	DOCOL
	DW	SUBB
	DW	ZEQU
	DW	SEMIS
;
	DB	81H
	DC	'<'
	DW	EQUAL-4
LESS:	DW	$+2
	POP	DE
	POP	HL
	LD	A,D
	XOR	H
	JP	M,LES1
	OR	A
	SBC	HL,DE
LES1:	INC	H
	DEC	H
	JP	M,LES2
	LD	HL,0
	JP	HPUSH
LES2:	LD	HL,1
	JP	HPUSH
;
	DB	82H
	DC	'U<'
	DW	LESS-4
ULESS:	DW	DOCOL,TDUP
	DW	XORR,ZLESS
	DW	ZBRAN,ULES1-$
	DW	DROP,ZLESS
	DW	ZEQU
	DW	BRAN,ULES2-$
ULES1:	DW	SUBB,ZLESS
ULES2:	DW	SEMIS
;
	DB	81H
	DC	'>'
	DW	ULESS-5
GREAT:	DW	DOCOL
	DW	SWAP
	DW	LESS
	DW	SEMIS
;
	DB	83H
	DC	'ROT'
	DW	GREAT-4
ROT:	DW	$+2
	POP	DE
	POP	HL
	EX	(SP),HL
	JP	DPUSH
;
	DB	85H
	DC	'SPACE'
	DW	ROT-6
SPACE:	DW	DOCOL
	DW	BL
	DW	EMIT
	DW	SEMIS
;
	DB	84H
	DC	'-DUP'
	DW	SPACE-8
DDUP:	DW	DOCOL
	DW	DUP
	DW	ZBRAN
	DW	DDUP1-$
	DW	DUP
DDUP1:	DW	SEMIS
;
	DB	88H
	DC	'TRAVERSE'
	DW	DDUP-7
TRAV:	DW	DOCOL
	DW	SWAP
TRAV1:	DW	OVER
	DW	PLUS
	DW	LIT
	DW	7FH
	DW	OVER
	DW	CAT
	DW	LESS
	DW	ZBRAN
	DW	TRAV1-$
	DW	SWAP
	DW	DROP
	DW	SEMIS
;
	DB	86H
	DC	'LATEST'
	DW	TRAV-0BH
LATES:	DW	DOCOL
	DW	CURR
	DW	AT
	DW	AT
	DW	SEMIS
;
	DB	83H
	DC	'LFA'
	DW	LATES-9
LFA:	DW	DOCOL
	DW	LIT
	DW	4
	DW	SUBB
	DW	SEMIS
;
	DB	83H
	DC	'CFA'
	DW	LFA-6
CFA:	DW	DOCOL
	DW	TWO
	DW	SUBB
	DW	SEMIS
;
	DB	83H
	DC	'NFA'
	DW	CFA-6
NFA:	DW	DOCOL
	DW	LIT
	DW	5
	DW	SUBB
	DW	LIT
	DW	-1
	DW	TRAV
	DW	SEMIS
;
	DB	83H
	DC	'PFA'
	DW	NFA-6
PFA:	DW	DOCOL
	DW	ONE
	DW	TRAV
	DW	LIT
	DW	5
	DW	PLUS
	DW	SEMIS
;
	DB	84H
	DC	'!CSP'
	DW	PFA-6
SCSP:	DW	DOCOL
	DW	SPAT
	DW	CSPP
	DW	STORE
	DW	SEMIS
;
	DB	86H
	DC	'?ERROR'
	DW	SCSP-7
QERR:	DW	DOCOL
	DW	SWAP
	DW	ZBRAN
	DW	QERR1-$
	DW	ERROR
	DW	BRAN
	DW	QERR2-$
QERR1:	DW	DROP
QERR2:	DW	SEMIS
;
	DB	85H
	DC	'?COMP'
	DW	QERR-9
QCOMP:	DW	DOCOL
	DW	STATE
	DW	AT
	DW	ZEQU
	DW	LIT
	DW	11H
	DW	QERR
	DW	SEMIS
;
	DB	85H
	DC	'?EXEC'
	DW	QCOMP-8
QEXEC:	DW	DOCOL
	DW	STATE
	DW	AT
	DW	LIT
	DW	12H
	DW	QERR
	DW	SEMIS
;
	DB	86H
	DC	'?PAIRS'
	DW	QEXEC-8
QPAIR:	DW	DOCOL
	DW	SUBB
	DW	LIT
	DW	13H
	DW	QERR
	DW	SEMIS
;
	DB	84H
	DC	'?CSP'
	DW	QPAIR-9
QCSP:	DW	DOCOL
	DW	SPAT
	DW	CSPP
	DW	AT
	DW	SUBB
	DW	LIT
	DW	14H
	DW	QERR
	DW	SEMIS
;
	DB	88H
	DC	'?LOADING'
	DW	QCSP-7
QLOAD:	DW	DOCOL
	DW	BLK
	DW	AT
	DW	ZEQU
	DW	LIT
	DW	16H
	DW	QERR
	DW	SEMIS
;
	DB	87H
	DC	'COMPILE'
	DW	QLOAD-0BH
COMP:	DW	DOCOL
	DW	QCOMP
	DW	FROMR
	DW	DUP
	DW	TWOP
	DW	TOR
	DW	AT
	DW	COMMA
	DW	SEMIS
;
	DB	0C1H
	DC	'['
	DW	COMP-0AH
LBRAC:	DW	DOCOL
	DW	ZERO
	DW	STATE
	DW	STORE
	DW	SEMIS
;
	DB	81H
	DC	']'
	DW	LBRAC-4
RBRAC:	DW	DOCOL
	DW	LIT,0C0H
	DW	STATE,STORE
	DW	SEMIS
;
	DB	86H
	DC	'SMUDGE'
	DW	RBRAC-4
SMUDG:	DW	DOCOL
	DW	LATES
	DW	LIT
	DW	20H
	DW	TOGGL
	DW	SEMIS
;
	DB	83H
	DC	'HEX'
	DW	SMUDG-9
HEX:	DW	DOCOL
	DW	LIT
	DW	10H
	DW	BASE
	DW	STORE
	DW	SEMIS
;
	DB	87H
	DC	'DECIMAL'
	DW	HEX-6
DEC:	DW	DOCOL
	DW	LIT
	DW	0AH
	DW	BASE
	DW	STORE
	DW	SEMIS
;
	DB	87H
	DC	'(;CODE)'
	DW	DEC-0AH
PSCOD:	DW	DOCOL
	DW	FROMR
	DW	LATES
	DW	PFA
	DW	CFA
	DW	STORE
	DW	SEMIS
;
	DB	0C5H
	DC	';CODE'
	DW	PSCOD-0AH
SEMIC:	DW	DOCOL
	DW	QCSP
	DW	COMP
	DW	PSCOD
	DW	LBRAC
SEMI1:	DW	NOOP
	DW	SEMIS
;
	DB	87H
	DC	'<BUILDS'
	DW	SEMIC-8
BUILD:	DW	DOCOL
	DW	ZERO
	DW	CON
	DW	SEMIS
;
	DB	85H
	DC	'DOES>'
	DW	BUILD-0AH
DOES:	DW	DOCOL
	DW	FROMR
	DW	LATES
	DW	PFA
	DW	STORE
	DW	PSCOD
DODOE:	LD	HL,(RPP)
	DEC	HL
	LD	(HL),B
	DEC	HL
	LD	(HL),C
	LD	(RPP),HL
	INC	DE
	EX	DE,HL
	LD	C,(HL)
	INC	HL
	LD	B,(HL)
	INC	HL
	JP	HPUSH
;
	DB	85H
	DC	'COUNT'
	DW	DOES-8
COUNT:	DW	DOCOL
	DW	DUP
	DW	ONEP
	DW	SWAP
	DW	CAT
	DW	SEMIS
;
	DB	84H
	DC	'TYPE'
	DW	COUNT-8
TYPE:	DW	DOCOL
	DW	DDUP
	DW	ZBRAN
	DW	TYPE1-$
	DW	OVER
	DW	PLUS
	DW	SWAP
	DW	XDO
TYPE2:	DW	IDO
	DW	CAT
	DW	EMIT
	DW	XLOOP
	DW	TYPE2-$
	DW	BRAN
	DW	TYPE3-$
TYPE1:	DW	DROP
TYPE3:	DW	SEMIS
;
	DB	89H
	DC	'-TRAILING'
	DW	TYPE-7
DTRAI:	DW	DOCOL
	DW	DUP
	DW	ZERO
	DW	XDO
DTRA1:	DW	OVER
	DW	OVER
	DW	PLUS
	DW	ONE
	DW	SUBB
	DW	CAT
	DW	BL
	DW	SUBB
	DW	ZBRAN
	DW	DTRA2-$
	DW	LEAVE
	DW	BRAN
	DW	DTRA3-$
DTRA2:	DW	ONE
	DW	SUBB
DTRA3:	DW	XLOOP
	DW	DTRA1-$
	DW	SEMIS
;
	DB	84H
	DC	'(.")'
	DW	DTRAI-0CH
PDOTQ:	DW	DOCOL
	DW	RR
	DW	COUNT
	DW	DUP
	DW	ONEP
	DW	FROMR
	DW	PLUS
	DW	TOR
	DW	TYPE
	DW	SEMIS
;
	DB	0C2H
	DC	'."'
	DW	PDOTQ-7
DOTQ:	DW	DOCOL
	DW	LIT
	DW	22H
	DW	STATE
	DW	AT
	DW	ZBRAN
	DW	DOTQ1-$
	DW	COMP
	DW	PDOTQ
	DW	WORD
	DW	HERE
	DW	CAT
	DW	ONEP
	DW	ALLOT
	DW	BRAN
	DW	DOTQ2-$
DOTQ1:	DW	WORD
	DW	HERE
	DW	COUNT
	DW	TYPE
DOTQ2:	DW	SEMIS
;
	DB	86H
	DC	'EXPECT'
	DW	DOTQ-5
EXPEC:	DW	DOCOL
	DW	OVER
	DW	PLUS
	DW	OVER
	DW	XDO
EXPE1:	DW	KEY
	DW	DUP
	DW	LIT
	DW	0EH
	DW	PORIG
	DW	AT
	DW	EQUAL
	DW	ZBRAN
	DW	EXPE2-$
	DW	DROP
	DW	DUP
	DW	IDO
	DW	EQUAL
	DW	DUP
	DW	FROMR
	DW	TWO
	DW	SUBB
	DW	PLUS
	DW	TOR
	DW	ZBRAN
	DW	EXPE6-$
	DW	LIT
	DW	BELL
	DW	BRAN
	DW	EXPE7-$
EXPE6:	DW	LIT
	DW	BSOUT
EXPE7:	DW	BRAN
	DW	EXPE3-$
EXPE2:	DW	DUP
	DW	LIT
	DW	0DH
	DW	EQUAL
	DW	ZBRAN
	DW	EXPE4-$
	DW	LEAVE
	DW	DROP
	DW	BL
	DW	ZERO
	DW	BRAN
	DW	EXPE5-$
EXPE4:	DW	DUP
EXPE5:	DW	IDO
	DW	CSTOR
	DW	ZERO
	DW	IDO
	DW	ONEP
	DW	STORE
EXPE3:	DW	EMIT
	DW	XLOOP
	DW	EXPE1-$
	DW	DROP
	DW	SEMIS
;
	DB	85H
	DC	'QUERY'
	DW	EXPEC-9
QUERY:	DW	DOCOL
	DW	TIB
	DW	AT
	DW	LIT
	DW	50H
	DW	EXPEC
	DW	ZERO
	DW	INN
	DW	STORE
	DW	SEMIS
;
	DB	0C1H
	DB	80H	;NULL
	DW	QUERY-8
NULL:	DW	DOCOL
	DW	BLK
	DW	AT
	DW	ZBRAN
	DW	NULL1-$
	DW	ONE
	DW	BLK
	DW	PSTOR
	DW	ZERO
	DW	INN
	DW	STORE
	DW	BLK
	DW	AT
	DW	BSCR
	DW	ONE
	DW	SUBB
	DW	ANDD
	DW	ZEQU
	DW	ZBRAN
	DW	NULL2-$
	DW	QEXEC
	DW	FROMR
	DW	DROP
NULL2:	DW	BRAN
	DW	NULL3-$
NULL1:	DW	FROMR
	DW	DROP
NULL3:	DW	SEMIS
;
	DB	84H
	DC	'FILL'
	DW	NULL-4
FILL:	DW	$+2
	LD	L,C
	LD	H,B
	POP	DE
	POP	BC
	EX	(SP),HL
	EX	DE,HL
FILL1:	LD	A,B
	OR	C
	JP	Z,FILL2
	LD	A,L
	LD	(DE),A
	INC	DE
	DEC	BC
	JP	FILL1
FILL2:	POP	BC
	JP	NEXT
;
	DB	85H
	DC	'ERASE'
	DW	FILL-7
ERASEE:	DW	DOCOL
	DW	ZERO
	DW	FILL
	DW	SEMIS
;
	DB	86H
	DC	'BLANKS'
	DW	ERASEE-8
BLANK:	DW	DOCOL
	DW	BL
	DW	FILL
	DW	SEMIS
;
	DB	84H
	DC	'HOLD'
	DW	BLANK-9
HOLD:	DW	DOCOL
	DW	LIT
	DW	-1
	DW	HLD
	DW	PSTOR
	DW	HLD
	DW	AT
	DW	CSTOR
	DW	SEMIS
;
	DB	83H
	DC	'PAD'
	DW	HOLD-7
PAD:	DW	DOCOL
	DW	HERE
	DW	LIT
	DW	44H
	DW	PLUS
	DW	SEMIS
;
	DB	84H
	DC	'WORD'
	DW	PAD-6
WORD:	DW	DOCOL
	DW	BLK
	DW	AT
	DW	ZBRAN
	DW	WORD1-$
	DW	BLK
	DW	AT
	DW	BLOCK
	DW	BRAN
	DW	WORD2-$
WORD1:	DW	TIB
	DW	AT
WORD2:	DW	INN
	DW	AT
	DW	PLUS
	DW	SWAP
	DW	ENCL
	DW	HERE
	DW	LIT
	DW	22H
	DW	BLANK
	DW	INN
	DW	PSTOR
	DW	OVER
	DW	SUBB
	DW	TOR
	DW	RR
	DW	HERE
	DW	CSTOR
	DW	PLUS
	DW	HERE
	DW	ONEP
	DW	FROMR
	DW	CMOVE
	DW	SEMIS
;
	DB	88H
	DC	'(NUMBER)'
	DW	WORD-7
PNUMB:	DW	DOCOL
PNUM1:	DW	ONEP
	DW	DUP
	DW	TOR
	DW	CAT
	DW	BASE
	DW	AT
	DW	DIGIT
	DW	ZBRAN
	DW	PNUM2-$
	DW	SWAP
	DW	BASE
	DW	AT
	DW	USTAR
	DW	DROP
	DW	ROT
	DW	BASE
	DW	AT
	DW	USTAR
	DW	DPLUS
	DW	DPL
	DW	AT
	DW	ONEP
	DW	ZBRAN
	DW	PNUM3-$
	DW	ONE
	DW	DPL
	DW	PSTOR
PNUM3:	DW	FROMR
	DW	BRAN
	DW	PNUM1-$
PNUM2:	DW	FROMR
	DW	SEMIS
;
	DB	86H
	DC	'NUMBER'
	DW	PNUMB-0BH
NUMB:	DW	DOCOL
	DW	ZERO
	DW	ZERO
	DW	ROT
	DW	DUP
	DW	ONEP
	DW	CAT
	DW	LIT
	DW	2DH
	DW	EQUAL
	DW	DUP
	DW	TOR
	DW	PLUS
	DW	LIT
	DW	-1
NUMB1:	DW	DPL
	DW	STORE
	DW	PNUMB
	DW	DUP
	DW	CAT
	DW	BL
	DW	SUBB
	DW	ZBRAN
	DW	NUMB2-$
	DW	DUP
	DW	CAT
	DW	LIT
	DW	2EH
	DW	SUBB
	DW	ZERO
	DW	QERR
	DW	ZERO
	DW	BRAN
	DW	NUMB1-$
NUMB2:	DW	DROP
	DW	FROMR
	DW	ZBRAN
	DW	NUMB3-$
	DW	DMINU
NUMB3:	DW	SEMIS
;
	DB	85H
	DC	'-FIND'
	DW	NUMB-9
DFIND:	DW	DOCOL
	DW	BL
	DW	WORD
	DW	HERE
	DW	CONT
	DW	AT
	DW	AT
	DW	PFIND
	DW	DUP
	DW	ZEQU
	DW	ZBRAN
	DW	DFIN1-$
	DW	DROP
	DW	HERE
	DW	LATES
	DW	PFIND
DFIN1:	DW	SEMIS
;
	DB	87H
	DC	'(ABORT)'
	DW	DFIND-8
PABOR:	DW	DOCOL
	DW	ABORT
	DW	SEMIS
;
	DB	85H
	DC	'ERROR'
	DW	PABOR-0AH
ERROR:	DW	DOCOL
	DW	WARN
	DW	AT
	DW	ZLESS
	DW	ZBRAN
	DW	ERRO1-$
	DW	PABOR
ERRO1:	DW	HERE
	DW	COUNT
	DW	TYPE
	DW	PDOTQ
	DB	2
	DC	'? '
	DW	MESS
	DW	SPSTO
	DW	BLK,AT
	DW	DDUP
	DW	ZBRAN,ERRO2-$
	DW	INN,AT
	DW	SWAP
ERRO2:	DW	QUIT
;
	DB	83H
	DC	'ID.'
	DW	ERROR-8
IDDOT:	DW	DOCOL
	DW	PAD
	DW	LIT
	DW	20H
	DW	LIT
	DW	5FH
	DW	FILL
	DW	DUP
	DW	PFA
	DW	LFA
	DW	OVER
	DW	SUBB
	DW	PAD
	DW	SWAP
	DW	CMOVE
	DW	PAD
	DW	COUNT
	DW	LIT
	DW	1FH
	DW	ANDD
	DW	TYPE
	DW	SPACE
	DW	SEMIS
;
	DB	86H
	DC	'CREATE'
	DW	IDDOT-6
CREAT:	DW	DOCOL
	DW	DFIND
	DW	ZBRAN
	DW	CREA1-$
	DW	DROP
	DW	NFA
	DW	IDDOT
	DW	LIT
	DW	4
	DW	MESS
	DW	SPACE
CREA1:	DW	HERE
	DW	DUP
	DW	CAT
	DW	WIDTH
	DW	AT
	DW	MIN
	DW	ONEP
	DW	ALLOT
	DW	DUP
	DW	LIT
	DW	0A0H
	DW	TOGGL
	DW	HERE
	DW	ONE
	DW	SUBB
	DW	LIT
	DW	80H
	DW	TOGGL
	DW	LATES
	DW	COMMA
	DW	CURR
	DW	AT
	DW	STORE
	DW	HERE
	DW	TWOP
	DW	COMMA
	DW	SEMIS
;
	DB	0C9H
	DC	'[COMPILE]'
	DW	CREAT-9
BCOMP:	DW	DOCOL
	DW	DFIND
	DW	ZEQU
	DW	ZERO
	DW	QERR
	DW	DROP
	DW	CFA
	DW	COMMA
	DW	SEMIS
;
	DB	0C7H
	DC	'LITERAL'
	DW	BCOMP-0CH
LITER:	DW	DOCOL
	DW	STATE
	DW	AT
	DW	ZBRAN
	DW	LITE1-$
	DW	COMP
	DW	LIT
	DW	COMMA
LITE1:	DW	SEMIS
;
	DB	0C8H
	DC	'DLITERAL'
	DW	LITER-0AH
DLITE:	DW	DOCOL
	DW	STATE
	DW	AT
	DW	ZBRAN
	DW	DLIT1-$
	DW	SWAP
	DW	LITER
	DW	LITER
DLIT1:	DW	SEMIS
;
	DB	86H
	DC	'?STACK'
	DW	DLITE-0BH
QSTAC:	DW	DOCOL
	DW	SPAT
	DW	SZERO
	DW	AT
	DW	SWAP
	DW	ULESS
	DW	ONE
	DW	QERR
	DW	SPAT
	DW	HERE
	DW	LIT
	DW	80H
	DW	PLUS
	DW	ULESS
	DW	LIT
	DW	7
	DW	QERR
	DW	SEMIS
;
	DB	89H
	DC	'INTERPRET'
	DW	QSTAC-9
INTER:	DW	DOCOL
INTE1:	DW	DFIND
	DW	ZBRAN
	DW	INTE2-$
	DW	STATE
	DW	AT
	DW	LESS
	DW	ZBRAN
	DW	INTE3-$
	DW	CFA
	DW	COMMA
	DW	BRAN
	DW	INTE4-$
INTE3:	DW	CFA
	DW	EXEC
INTE4:	DW	QSTAC
	DW	BRAN
	DW	INTE5-$
INTE2:	DW	HERE
	DW	NUMB
	DW	DPL
	DW	AT
	DW	ONEP
	DW	ZBRAN
	DW	INTE6-$
	DW	DLITE
	DW	BRAN
	DW	INTE7-$
INTE6:	DW	DROP
	DW	LITER
INTE7:	DW	QSTAC
INTE5:	DW	BRAN
	DW	INTE1-$
;
;
	DB	89H
	DC	'IMMEDIATE'
	DW	INTER-0CH
IMMED:	DW	DOCOL
	DW	LATES
	DW	LIT
	DW	40H
	DW	TOGGL
	DW	SEMIS
;
	DB	8AH
	DC	'VOCABULARY'
	DW	IMMED-0CH
VOCAB:	DW	DOCOL
	DW	BUILD
	DW	LIT
	DW	0A081H
	DW	COMMA
	DW	CURR
	DW	AT
	DW	CFA
	DW	COMMA
	DW	HERE
	DW	VOCL
	DW	AT
	DW	COMMA
	DW	VOCL
	DW	STORE
	DW	DOES
DOVOC:	DW	TWOP
	DW	CONT
	DW	STORE
	DW	SEMIS
;
	DB	0C5H
	DC	'FORTH'
	DW	VOCAB-0DH
FORTH:	DW	DODOE
	DW	DOVOC
	DW	0A081H
	DW	TASK-7	;!COLD START ONLY
	DW	0
;
	DB	8BH
	DC	'DEFINITIONS'
	DW	FORTH-8
DEFIN:	DW	DOCOL
	DW	CONT
	DW	AT
	DW	CURR
	DW	STORE
	DW	SEMIS
;
	DB	0C1H
	DC	'('
	DW	DEFIN-0EH
PAREN:	DW	DOCOL
	DW	LIT
	DW	29H
	DW	WORD
	DW	SEMIS
;
	DB	84H
	DC	'QUIT'
	DW	PAREN-4
QUIT:	DW	DOCOL
	DW	ZERO
	DW	BLK
	DW	STORE
	DW	LBRAC
QUIT1:	DW	RPSTO
	DW	CR
	DW	QUERY
	DW	INTER
	DW	STATE
	DW	AT
	DW	ZEQU
	DW	ZBRAN
	DW	QUIT2-$
	DW	PDOTQ
	DB	2
	DC	'OK'
QUIT2:	DW	BRAN
	DW	QUIT1-$
;
	DB	85H
	DC	'ABORT'
	DW	QUIT-7
ABORT:	DW	DOCOL
	DW	SPSTO
	DW	DEC
	DW	QSTAC
	DW	CR
	DW	DOTCPU
	DW	PDOTQ
	DB	0DH
	DC	'FIG-FORTH '
	DB	FIGREL+30H,ADOT,FIGREV+30H
	DW	FORTH
	DW	DEFIN
	DW	QUIT
;
WRM:	LD	BC,WRM1
	JP	NEXT
WRM1:	DW	WARM
;
	DB	84H
	DC	'WARM'
	DW	ABORT-8
WARM:	DW	DOCOL
	DW	MTBUF
	DW	ABORT
;
CLD:	LD	BC,CLD1
	LD	HL,(ORIG+12H)	;!
	LD	SP,HL
	JP	NEXT
CLD1:	DW	COLD
;
	DB	84H
	DC	'COLD'
	DW	WARM-7
COLD:	DW	DOCOL
	DW	MTBUF
	DW	ZERO,DENSTY
	DW	STORE
	DW	LIT,BUF1
	DW	USE,STORE
	DW	LIT,BUF1
	DW	PREV,STORE
	DW	DRZER
	DW	LIT,0
	DW	LIT,EPRINT
	DW	STORE
	DW	LIT
	DW	ORIG+12H
	DW	LIT
	DW	UP
	DW	AT
	DW	LIT
	DW	6
	DW	PLUS
	DW	LIT
	DW	10H
	DW	CMOVE
	DW	LIT
	DW	ORIG+0CH
	DW	AT
	DW	LIT
	DW	FORTH+6
	DW	STORE
	DW	LIT,250
	DW	OFSET,STORE
	DW	ABORT
;
	DB	84H
	DC	'S->D'
	DW	COLD-7
STOD:	DW	$+2
	POP	DE
	LD	HL,0
	LD	A,D
	AND	80H
	JP	Z,STOD1
	DEC	HL
STOD1:	JP	DPUSH
;
	DB	82H
	DC	'+-'
	DW	STOD-7
PM:	DW	DOCOL
	DW	ZLESS
	DW	ZBRAN
	DW	PM1-$
	DW	MINUS
PM1:	DW	SEMIS
;
	DB	83H
	DC	'D+-'
	DW	PM-5
DPM:	DW	DOCOL
	DW	ZLESS
	DW	ZBRAN
	DW	DPM1-$
	DW	DMINU
DPM1:	DW	SEMIS
;
	DB	83H
	DC	'ABS'
	DW	DPM-6
ABS:	DW	DOCOL
	DW	DUP
	DW	PM
	DW	SEMIS
;
	DB	84H
	DC	'DABS'
	DW	ABS-6
DABS:	DW	DOCOL
	DW	DUP
	DW	DPM
	DW	SEMIS
;
	DB	83H
	DC	'MIN'
	DW	DABS-7
MIN:	DW	DOCOL,TDUP
	DW	GREAT
	DW	ZBRAN
	DW	MIN1-$
	DW	SWAP
MIN1:	DW	DROP
	DW	SEMIS
;
	DB	83H
	DC	'MAX'
	DW	MIN-6
MAX:	DW	DOCOL,TDUP
	DW	LESS
	DW	ZBRAN
	DW	MAX1-$
	DW	SWAP
MAX1:	DW	DROP
	DW	SEMIS
;
	DB	82H
	DC	'M*'
	DW	MAX-6
MSTAR:	DW	DOCOL,TDUP
	DW	XORR
	DW	TOR
	DW	ABS
	DW	SWAP
	DW	ABS
	DW	USTAR
	DW	FROMR
	DW	DPM
	DW	SEMIS
;
	DB	82H
	DC	'M/'
	DW	MSTAR-5
MSLAS:	DW	DOCOL
	DW	OVER
	DW	TOR
	DW	TOR
	DW	DABS
	DW	RR
	DW	ABS
	DW	USLAS
	DW	FROMR
	DW	RR
	DW	XORR
	DW	PM
	DW	SWAP
	DW	FROMR
	DW	PM
	DW	SWAP
	DW	SEMIS
;
	DB	81H
	DC	'*'
	DW	MSLAS-5
STAR:	DW	DOCOL
	DW	MSTAR
	DW	DROP
	DW	SEMIS
;
	DB	84H
	DC	'/MOD'
	DW	STAR-4
SLMOD:	DW	DOCOL
	DW	TOR
	DW	STOD
	DW	FROMR
	DW	MSLAS
	DW	SEMIS
;
	DB	81H
	DC	'/'
	DW	SLMOD-7
SLASH:	DW	DOCOL
	DW	SLMOD
	DW	SWAP
	DW	DROP
	DW	SEMIS
;
	DB	83H
	DC	'MOD'
	DW	SLASH-4
MODD:	DW	DOCOL
	DW	SLMOD
	DW	DROP
	DW	SEMIS
;
	DB	85H
	DC	'*/MOD'
	DW	MODD-6
SSMOD:	DW	DOCOL
	DW	TOR
	DW	MSTAR
	DW	FROMR
	DW	MSLAS
	DW	SEMIS
;
	DB	82H
	DC	'*/'
	DW	SSMOD-8
SSLA:	DW	DOCOL
	DW	SSMOD
	DW	SWAP
	DW	DROP
	DW	SEMIS
;
	DB	85H
	DC	'M/MOD'
	DW	SSLA-5
MSMOD:	DW	DOCOL
	DW	TOR
	DW	ZERO
	DW	RR
	DW	USLAS
	DW	FROMR
	DW	SWAP
	DW	TOR
	DW	USLAS
	DW	FROMR
	DW	SEMIS
;
	DB	86H
	DC	'(LINE)'
	DW	MSMOD-8
PLINE:	DW	DOCOL
	DW	TOR
	DW	LIT
	DW	40H
	DW	BBUF
	DW	SSMOD
	DW	FROMR
	DW	BSCR
	DW	STAR
	DW	PLUS
	DW	BLOCK
	DW	PLUS
	DW	LIT
	DW	40H
	DW	SEMIS
;
	DB	85H
	DC	'.LINE'
	DW	PLINE-9
DLINE:	DW	DOCOL
	DW	PLINE
	DW	DTRAI
	DW	TYPE
	DW	SEMIS
;
	DB	87H
	DC	'MESSAGE'
	DW	DLINE-8
MESS:	DW	DOCOL
	DW	WARN
	DW	AT
	DW	ZBRAN
	DW	MESS1-$
	DW	DDUP
	DW	ZBRAN
	DW	MESS2-$
	DW	LIT
	DW	4
	DW	DLINE
	DW	SPACE
MESS2:	DW	BRAN
	DW	MESS3-$
MESS1:	DW	PDOTQ
	DB	6
	DC	'MSG # '
	DW	DOT
MESS3:	DW	SEMIS
;
;	PORT I/O
;
	DB	82H
	DC	'P@'
	DW	MESS-0AH
PTAT:	DW	$+2	; Modified to be reentrant
	EXX
	POP	BC
	IN	L,(C)
	LD	H,0
	PUSH	HL
	EXX
	JP	NEXT
;
	DB	82H
	DC	'P!'
	DW	PTAT-5
PTSTO:	DW	$+2	; modified to be reentrant
	EXX
	POP	BC
	POP	HL
	OUT	(C),L
	EXX
	JP	NEXT
;
;	CP/M INTERFACE
CDOS	EQU 5
RITSEC	EQU	39
RDSEC	EQU	36
SETDMA	EQU	33
SETSEC	EQU	30
SETTRK	EQU	27
SETDSK	EQU	24
;	DENSITIES (5 INCH)
SPT2	EQU	52
TRKS2	EQU	77
SPDRV2	EQU	SPT2*TRKS2
;
SPT1	EQU	26
TRKS1	EQU	77
SPDRV1	EQU	SPT1*TRKS1
;
BPS	EQU	128
MXDRV	EQU	2
;
	DB	85H
	DC	'DRIVE'
	DW	PTSTO-5
DRIVE:	DW	DOVAR,0
;
	DB	83H
	DC	'SEC'
	DW	DRIVE-8
SEC:	DW	DOVAR,0
;
	DB	85H
	DC	'TRACK'
	DW	SEC-6
TRACK:	DW	DOVAR,0
;
	DB	83H
	DC	'USE'
	DW	TRACK-8
USE:	DW	DOVAR
	DW	BUF1
;
	DB	84H
	DC	'PREV'
	DW	USE-6
PREV:	DW	DOVAR
	DW	BUF1
;
	DB	87H
	DC	'SEC/BLK'
	DW	PREV-7
SPBLK:	DW	DOCON
	DW	KBBUF/BPS
;
	DB	85H
	DC	'#BUFF'
	DW	SPBLK-10
NOBUF:	DW	DOCON,NBUF
;
	DB	87H
	DC	'DENSITY'
	DW	NOBUF-8
DENSTY:	DW	DOVAR,0
;
	DB	8AH
	DC	'DISK-ERROR'
	DW	DENSTY-10
DSKERR:	DW	DOVAR,0
;
;
	DB	84H
	DC	'+BUF'
	DW	DSKERR-13
PBUF:	DW	DOCOL
	DW	LIT,CO
	DW	PLUS,DUP
	DW	LIMIT,EQUAL
	DW	ZBRAN,PBUF1-$
	DW	DROP,FIRST
PBUF1:	DW	DUP,PREV
	DW	AT,SUBB
	DW	SEMIS
;
	DB	86H
	DC	'UPDATE'
	DW	PBUF-7
UPDAT:	DW	DOCOL,PREV
	DW	AT,AT
	DW	LIT,8000H
	DW	ORR
	DW	PREV,AT
	DW	STORE,SEMIS
;
	DB	8DH
	DC	'EMPTY-BUFFERS'
	DW	UPDAT-9
MTBUF:	DW	DOCOL,FIRST
	DW	LIMIT,OVER
	DW	SUBB,ERASEE
	DW	SEMIS
;
	DB	83H
	DC	'DR0'
	DW	MTBUF-16
DRZER:	DW	DOCOL
	DW	ZERO
	DW	OFSET,STORE
	DW	SEMIS
;
	DB	83H
	DC	'DR1'
	DW	DRZER-6
DRONE:	DW	DOCOL
	DW	DENSTY,AT
	DW	ZBRAN,DRON1-$
	DW	LIT,SPDRV2
	DW	BRAN,DRON2-$
DRON1:	DW	LIT,SPDRV1
DRON2:	DW	OFSET,STORE
	DW	SEMIS
;
	DB	86H
	DC	'BUFFER'
	DW	DRONE-6
BUFFE:	DW	DOCOL,USE
	DW	AT,DUP
	DW	TOR
BUFF1:	DW	PBUF
	DW	ZBRAN,BUFF1-$
	DW	USE,STORE
	DW	RR,AT
	DW	ZLESS
	DW	ZBRAN,BUFF2-$
	DW	RR,TWOP
	DW	RR,AT
	DW	LIT,7FFFH
	DW	ANDD,ZERO
	DW	RSLW
BUFF2:	DW	RR,STORE
	DW	RR,PREV
	DW	STORE,FROMR
	DW	TWOP,SEMIS
;
	DB	85H
	DC	'BLOCK'
	DW	BUFFE-9
BLOCK:	DW	DOCOL,OFSET
	DW	AT,PLUS
	DW	TOR,PREV
	DW	AT,DUP
	DW	AT,RR
	DW	SUBB
	DW	DUP,PLUS
	DW	ZBRAN,BLOC1-$
BLOC2:	DW	PBUF,ZEQU
	DW	ZBRAN,BLOC3-$
	DW	DROP,RR
	DW	BUFFE,DUP
	DW	RR,ONE
	DW	RSLW
	DW	TWO,SUBB
BLOC3:	DW	DUP,AT
	DW	RR,SUBB
	DW	DUP,PLUS
	DW	ZEQU
	DW	ZBRAN,BLOC2-$
	DW	DUP,PREV
	DW	STORE
BLOC1:	DW	FROMR,DROP
	DW	TWOP,SEMIS
;
;	CP/M INTERFACE
;
;		Service IOS request
;
IOS:	LD	HL,(1)
	ADD	HL,DE	; DE - Service request offset
	JP	(HL)
;
;
	DB	86H
	DC	'SET-IO'
	DW	BLOCK-8
SETIO:	DW	$+2
	PUSH	BC
	LD	HL,(USE+2)
	LD	B,H
	LD	C,L
	LD	DE,SETDMA
	CALL	IOS
	LD	HL,(SEC+2)
	LD	C,L
	LD	DE,SETSEC
	CALL	IOS
;
	LD	HL,(TRACK+2)
	LD	B,H
	LD	C,L
	LD	DE,SETTRK
	CALL	IOS
	POP	BC
	JP	NEXT
;
	DB	89H
	DC	'SET-DRIVE'
	DW	SETIO-9
SETDRV:	DW	$+2
	PUSH	BC
	LD	A,(DRIVE+2)
	LD	C,A
	LD	DE,SETDSK
	CALL	IOS
	POP	BC
	JP	NEXT
;
	DB	87H
	DC	'T&SCALC'
	DW	SETDRV-12
TSCALC:	DW	DOCOL,DENSTY
	DW	AT
	DW	ZBRAN,TSCALS-$
	DW	LIT,SPDRV2
	DW	SLMOD
	DW	LIT,MXDRV
	DW	MIN
	DW	DUP,DRIVE
	DW	AT,EQUAL
	DW	ZBRAN,TSCAL1-$
	DW	DROP
	DW	BRAN,TSCAL2-$
TSCAL1:	DW	DRIVE,STORE
	DW	SETDRV
TSCAL2:	DW	LIT,SPT2
	DW	SLMOD,TRACK
	DW	STORE,ONEP
	DW	SEC,STORE
	DW	SEMIS
;
TSCALS:	DW	LIT,SPDRV1
	DW	SLMOD
	DW	LIT,MXDRV
	DW	MIN
	DW	DUP,DRIVE
	DW	AT,EQUAL
	DW	ZBRAN,TSCAL3-$
	DW	DROP
	DW	BRAN,TSCAL4-$
TSCAL3:	DW	DRIVE,STORE
	DW	SETDRV
TSCAL4:	DW	LIT,SPT1
	DW	SLMOD,TRACK
	DW	STORE,ONEP
	DW	SEC,STORE
	DW	SEMIS
;
	DB	88H
	DC	'SEC-READ'
	DW	TSCALC-10
SECRD:	DW	$+2
	PUSH	BC
	LD	DE,RDSEC
	CALL	IOS
	LD	(DSKERR+2),A
	POP	BC
	JP	NEXT
;
	DB	89H
	DC	'SEC-WRITE'
	DW	SECRD-11
SECWT:	DW	$+2
	PUSH	BC
	LD	DE,RITSEC
	CALL	IOS
	LD	(DSKERR+2),A
	POP	BC
	JP	NEXT
;
;
;
	DB	83H
	DC	'R/W'
	DW	SECWT-12
RSLW:	DW	DOCOL
	DW	USE,AT
	DW	TOR
	DW	SWAP,SPBLK
	DW	STAR,ROT
	DW	USE,STORE
	DW	SPBLK,ZERO
	DW	XDO
RSLW1:	DW	OVER,OVER
	DW	TSCALC,SETIO
	DW	ZBRAN,RSLW2-$
	DW	SECRD
	DW	BRAN,RSLW3-$
RSLW2:	DW	SECWT
RSLW3:	DW	ONEP
	DW	LIT,80H
	DW	USE,PSTOR
	DW	XLOOP,RSLW1-$
	DW	DROP,DROP
	DW	FROMR,USE
	DW	STORE,SEMIS
;
	DB	85H
	DC	'FLUSH'
	DW	RSLW-6
FLUSH:	DW	DOCOL
	DW	NOBUF,ONEP
	DW	ZERO,XDO
FLUS1:	DW	ZERO,BUFFE
	DW	DROP
	DW	XLOOP,FLUS1-$
	DW	SEMIS
;
	DB	84H
	DC	'LOAD'
	DW	FLUSH-8
LOAD:	DW	DOCOL,BLK
	DW	AT,TOR
	DW	INN,AT
	DW	TOR,ZERO
	DW	INN,STORE
	DW	BSCR,STAR
	DW	BLK,STORE
	DW	INTER
	DW	FROMR,INN
	DW	STORE
	DW	FROMR,BLK
	DW	STORE
	DW	SEMIS
;
	DB	0C3H
	DC	'-->'
	DW	LOAD-7
ARROW:	DW	DOCOL
	DW	QLOAD
	DW	ZERO
	DW	INN
	DW	STORE
	DW	BSCR
	DW	BLK
	DW	AT
	DW	OVER
	DW	MODD
	DW	SUBB
	DW	BLK
	DW	PSTOR
	DW	SEMIS
;
;
;	CDOS CONSOLE INTERFACE
;
;	The ^P toggles printing (LST) device off and on
;
KCSTAT	EQU	3
KCIN	EQU	6
KCOUT	EQU	9
KPOUT	EQU	0CH
EPRINT:	DW	0
;
CSTAT:	PUSH	BC
	LD	DE,KCSTAT
	CALL	IOS
	POP	BC
	RET
;
CIN:	PUSH	BC
	LD	DE,KCIN
	CALL	IOS
	POP	BC
	RET
;
COUT:	PUSH	HL
	LD	DE,KCOUT
	CALL	IOS
	POP	HL
	RET
;
POUT:	LD	DE,KPOUT	; Printer output on ^P
	CALL	IOS
	RET
;
CPOUT:	CALL	COUT
	EX	DE,HL
	LD	HL,EPRINT
	LD	A,(HL)
	OR	A
	JR	Z,CPOU1
	LD	C,E
	CALL	POUT
CPOU1:	RET
;
PQTER:	CALL	CSTAT
	LD	HL,0
	OR	A
	JP	Z,PQTE1
	INC	L
PQTE1:	JP	HPUSH
;
PKEY:	CALL	CIN
	CP	DLE
	LD	E,A
	JP	NZ,PKEY1
	LD	HL,EPRINT
	LD	E,ABL
	LD	A,(HL)
	XOR	1
	LD	(HL),A
PKEY1:	LD	L,E
	LD	H,0
	JP	HPUSH
;
PEMIT:	DW	$+2
	POP	HL
	PUSH	BC
	LD	C,L
	CALL	CPOUT
	POP	BC
	JP	NEXT
;
PCR:	PUSH	BC
	LD	C,ACR
	LD	L,C
	CALL	CPOUT
	LD	C,LF
	LD	L,C
	CALL	CPOUT
	POP	BC
	JP	NEXT
;
	DB	0C1H	; '
	DB	0A7H
	DW	ARROW-6	;!!
TICK:	DW	DOCOL
	DW	DFIND
	DW	ZEQU
	DW	ZERO
	DW	QERR
	DW	DROP
	DW	LITER
	DW	SEMIS
;
	DB	86H
	DC	'FORGET'
	DW	TICK-4
FORG:	DW	DOCOL
	DW	CURR
	DW	AT
	DW	CONT
	DW	AT
	DW	SUBB
	DW	LIT
	DW	18H
	DW	QERR
	DW	TICK
	DW	DUP
	DW	FENCE
	DW	AT
	DW	LESS
	DW	LIT
	DW	15H
	DW	QERR
	DW	DUP
	DW	NFA
	DW	DP
	DW	STORE
	DW	LFA
	DW	AT
	DW	CONT
	DW	AT
	DW	STORE
	DW	SEMIS
;
	DB	84H
	DC	'BACK'
	DW	FORG-9
BACK:	DW	DOCOL
	DW	HERE
	DW	SUBB
	DW	COMMA
	DW	SEMIS
;
	DB	0C5H
	DC	'BEGIN'
	DW	BACK-7
BEGIN:	DW	DOCOL
	DW	QCOMP
	DW	HERE
	DW	ONE
	DW	SEMIS
;
	DB	0C5H
	DC	'ENDIF'
	DW	BEGIN-8
ENDIFF:	DW	DOCOL
	DW	QCOMP
	DW	TWO
	DW	QPAIR
	DW	HERE
	DW	OVER
	DW	SUBB
	DW	SWAP
	DW	STORE
	DW	SEMIS
;
	DB	0C4H
	DC	'THEN'
	DW	ENDIFF-8
THEN:	DW	DOCOL
	DW	ENDIFF
	DW	SEMIS
;
	DB	0C2H
	DC	'DO'
	DW	THEN-7
DO:	DW	DOCOL
	DW	COMP
	DW	XDO
	DW	HERE
	DW	THREE
	DW	SEMIS
;
	DB	0C4H
	DC	'LOOP'
	DW	DO-5
LOOP:	DW	DOCOL
	DW	THREE
	DW	QPAIR
	DW	COMP
	DW	XLOOP
	DW	BACK
	DW	SEMIS
;
	DB	0C5H
	DC	'+LOOP'
	DW	LOOP-7
PLOOP:	DW	DOCOL
	DW	THREE
	DW	QPAIR
	DW	COMP
	DW	XPLOO
	DW	BACK
	DW	SEMIS
;
	DB	0C5H
	DC	'UNTIL'
	DW	PLOOP-8
UNTIL:	DW	DOCOL
	DW	ONE
	DW	QPAIR
	DW	COMP
	DW	ZBRAN
	DW	BACK
	DW	SEMIS
;
	DB	0C3H
	DC	'END'
	DW	UNTIL-8
ENDD:	DW	DOCOL
	DW	UNTIL
	DW	SEMIS
;
	DB	0C5H
	DC	'AGAIN'
	DW	ENDD-6
AGAIN:	DW	DOCOL
	DW	ONE
	DW	QPAIR
	DW	COMP
	DW	BRAN
	DW	BACK
	DW	SEMIS
;
	DB	0C6H
	DC	'REPEAT'
	DW	AGAIN-8
REPEA:	DW	DOCOL
	DW	TOR
	DW	TOR
	DW	AGAIN
	DW	FROMR
	DW	FROMR
	DW	TWO
	DW	SUBB
	DW	ENDIFF
	DW	SEMIS
;
	DB	0C2H
	DC	'IF'
	DW	REPEA-9
IFF:	DW	DOCOL
	DW	COMP
	DW	ZBRAN
	DW	HERE
	DW	ZERO
	DW	COMMA
	DW	TWO
	DW	SEMIS
;
	DB	0C4H
	DC	'ELSE'
	DW	IFF-5
ELSEE:	DW	DOCOL
	DW	TWO
	DW	QPAIR
	DW	COMP
	DW	BRAN
	DW	HERE
	DW	ZERO
	DW	COMMA
	DW	SWAP
	DW	TWO
	DW	ENDIFF
	DW	TWO
	DW	SEMIS
;
	DB	0C5H
	DC	'WHILE'
	DW	ELSEE-7
WHILE:	DW	DOCOL
	DW	IFF
	DW	TWOP
	DW	SEMIS
;
	DB	86H
	DC	'SPACES'
	DW	WHILE-8
SPACS:	DW	DOCOL
	DW	ZERO
	DW	MAX
	DW	DDUP
	DW	ZBRAN
	DW	SPAX1-$
	DW	ZERO
	DW	XDO
SPAX2:	DW	SPACE
	DW	XLOOP
	DW	SPAX2-$
SPAX1:	DW	SEMIS
;
	DB	82H
	DC	'<#'
	DW	SPACS-9
BDIGS:	DW	DOCOL
	DW	PAD
	DW	HLD
	DW	STORE
	DW	SEMIS
;
	DB	82H
	DC	'#>'
	DW	BDIGS-5
EDIGS:	DW	DOCOL
	DW	DROP
	DW	DROP
	DW	HLD
	DW	AT
	DW	PAD
	DW	OVER
	DW	SUBB
	DW	SEMIS
;
	DB	84H
	DC	'SIGN'
	DW	EDIGS-5
SIGN:	DW	DOCOL
	DW	ROT
	DW	ZLESS
	DW	ZBRAN
	DW	SIGN1-$
	DW	LIT
	DW	2DH
	DW	HOLD
SIGN1:	DW	SEMIS
;
	DB	81H
	DC	'#'
	DW	SIGN-7
DIG:	DW	DOCOL
	DW	BASE
	DW	AT
	DW	MSMOD
	DW	ROT
	DW	LIT
	DW	9
	DW	OVER
	DW	LESS
	DW	ZBRAN
	DW	DIG1-$
	DW	LIT
	DW	7
	DW	PLUS
DIG1:	DW	LIT
	DW	30H
	DW	PLUS
	DW	HOLD
	DW	SEMIS
;
	DB	82H
	DC	'#S'
	DW	DIG-4
DIGS:	DW	DOCOL
DIGS1:	DW	DIG
	DW	OVER
	DW	OVER
	DW	ORR
	DW	ZEQU
	DW	ZBRAN
	DW	DIGS1-$
	DW	SEMIS
;
	DB	83H
	DC	'D.R'
	DW	DIGS-5
DDOTR:	DW	DOCOL
	DW	TOR
	DW	SWAP
	DW	OVER
	DW	DABS
	DW	BDIGS
	DW	DIGS
	DW	SIGN
	DW	EDIGS
	DW	FROMR
	DW	OVER
	DW	SUBB
	DW	SPACS
	DW	TYPE
	DW	SEMIS
;
	DB	82H
	DC	'.R'
	DW	DDOTR-6
DOTR:	DW	DOCOL
	DW	TOR
	DW	STOD
	DW	FROMR
	DW	DDOTR
	DW	SEMIS
;
	DB	82H
	DC	'D.'
	DW	DOTR-5
DDOT:	DW	DOCOL
	DW	ZERO
	DW	DDOTR
	DW	SPACE
	DW	SEMIS
;
	DB	81H
	DC	'.'
	DW	DDOT-5
DOT:	DW	DOCOL
	DW	STOD
	DW	DDOT
	DW	SEMIS
;
	DB	81H
	DC	'?'
	DW	DOT-4
QUES:	DW	DOCOL
	DW	AT
	DW	DOT
	DW	SEMIS
;
	DB	82H
	DC	'U.'
	DW	QUES-4
UDOT:	DW	DOCOL
	DW	ZERO
	DW	DDOT
	DW	SEMIS
;
	DB	85H
	DC	'VLIST'
	DW	UDOT-5
VLIST:	DW	DOCOL
	DW	LIT
	DW	80H
	DW	OUTT
	DW	STORE
	DW	CONT
	DW	AT
	DW	AT
VLIS1:	DW	OUTT
	DW	AT
	DW	CSLL
	DW	GREAT
	DW	ZBRAN
	DW	VLIS2-$
	DW	CR
	DW	ZERO
	DW	OUTT
	DW	STORE
VLIS2:	DW	DUP
	DW	IDDOT
	DW	SPACE
	DW	SPACE
	DW	PFA
	DW	LFA
	DW	AT
	DW	DUP
	DW	ZEQU
	DW	QTERM
	DW	ORR
	DW	ZBRAN
	DW	VLIS1-$
	DW	DROP
	DW	SEMIS
;
	DB	83H
	DC	'BYE'
	DW	VLIST-8
BYE:	DW	$+2
	JP	0
;
;
;
	DB	84H
	DC	'LIST'
	DW	BYE-6
LIST:	DW	DOCOL,BASE,AT
	DW	TOR,DEC
	DW	CR,DUP
	DW	SCR,STORE
	DW	PDOTQ
	DB	6,'SCR # '
	DW	DOT
	DW	LIT,10H
	DW	ZERO,XDO
LIST1:	DW	CR,IDO
	DW	LIT,3
	DW	DOTR,SPACE
	DW	IDO,SCR
	DW	AT,DLINE
	DW	QTERM
	DW	ZBRAN,LIST2-$
	DW	LEAVE
LIST2:	DW	XLOOP,LIST1-$
	DW	FROMR,BASE,STORE
	DW	CR,SEMIS
;
	DB	85H
	DC	'INDEX'
	DW	LIST-7
INDEX:	DW	DOCOL
	DW	LIT,FF
	DW	EMIT,CR
	DW	ONEP,SWAP
	DW	XDO
INDE1:	DW	CR,IDO
	DW	LIT,3
	DW	DOTR,SPACE
	DW	ZERO,IDO
	DW	DLINE,QTERM
	DW	ZBRAN,INDE2-$
	DW	LEAVE
INDE2:	DW	XLOOP,INDE1-$
	DW	SEMIS
;
	DB	85H
	DC	'TRIAD'
	DW	INDEX-8
TRIAD:	DW	DOCOL
	DW	LIT,FF
	DW	EMIT
	DW	LIT,3
	DW	SLASH
	DW	LIT,3
	DW	STAR
	DW	LIT,3
	DW	OVER,PLUS
	DW	SWAP,XDO
TRIA1:	DW	CR,IDO
	DW	LIST
	DW	QTERM
	DW	ZBRAN,TRIA2-$
	DW	LEAVE
TRIA2:	DW	XLOOP,TRIA1-$
	DW	CR
	DW	LIT,15
	DW	MESS,CR
	DW	SEMIS
;
	DB	84H
	DC	'.CPU'
	DW	TRIAD-8
DOTCPU:	DW	DOCOL
	DW	PDOTQ
	DB	5
	DC	'Z-80 '
	DW	SEMIS
;
	DB	86H
	DC	'VERIFY'
	DW	DOTCPU-7
VERIF:	DW	$+2
	EXX
	POP	BC
	POP	DE
	POP	HL
	LD	A,B
	OR	C
	JR	Z,VERI2
VERIA:	LD	A,(DE)
	CPI
	JR	NZ,VERI2
	INC	DE
	JP	PE,VERIA
	EXX
	LD	HL,1
	JP	HPUSH
VERI2:	EXX
	LD	HL,0
	JP	HPUSH
; SPECIAL MATCHING FUNCTION
;
	DB	84H
	DC	'TASK'
	DW	VERIF-9
TASK:	DW	DOCOL
	DW	SEMIS
;
INITDP	EQU	EM-$
;
	END	ORIG
