10 REM AMODEM3.BAS:VER3.0:06-01-82
20 GOTO 10000
990 SETCOLOR 2,7,2:? :? "*** TERMINAL MODE ***"
1000 ? :? " OPTION  = DISK DIRECTORY"
1010 ? " SELECT  = S/R FILE NAME"
1020 ? " START   = START TRANSMISSION"
1030 SETCOLOR 2,7,2:C$=CHR$(SRFLAG):IF SRFLAG=ZERO THEN FILE$=P$:C$=" "
1040 ? C$;" FILE   = ";FILE$:? :? "*** TERMINAL MODE ***"
1050 STATUS #MODEM,STAT:IF  NOT PEEK(747) THEN 1090
1060 GET #MODEM,C:IF C>31 THEN ? CHR$(C);:GOTO TERM
1070 IF C=BEL THEN ? CHR$(253);:GOTO TERM
1080 IF C=BS THEN ? CHR$(126);:GOTO TERM
1090 IF PEEK(CON)<>7 THEN 1200
1100 IF PEEK(764)=255 THEN GOTO TERM
1110 GET #KEY,C
1120 IF C=126 THEN C=BS
1130 PUT #MODEM,C:GOTO TERM
1200 IF PEEK(CON)=6 THEN 5000
1210 IF PEEK(CON)=5 THEN 6000
1220 IF PEEK(CON)=3 THEN 4000
1250 GOTO TERM
2000 TRAN=32:GOSUB IO:A=NAK
2010 SETCOLOR 2,4,2:BLOCK=ZERO
2020 ? :? "*** RECEIVING ";FILE$;" ***"
2300 FOR TRY=WON TO ERRTRY-WON
2310 ? :? "*** GETTING SECTOR ";BLOCK+WON;"/";TRY;" ***"
2320 PUT #MODEM,A:A=ACK
2330 GET #MODEM,SH:SUM=SH:IF SH=EOT OR SH=CAN THEN 2380
2340 GET #MODEM,C:SUM=SUM+C:GET #MODEM,C:SUM=SUM+C
2350 ADDR=BLOCK*128+BUFF:FOR BLK=0 TO 127:GET #MODEM,C:POKE ADDR+BLK,C:? CHR$(C);:SUM=SUM+C:NEXT BLK
2360 GET #MODEM,C:SUM=ASC(CHR$(SUM)):IF C=SUM THEN 2380
2370 A=NAK:FOR C=WON TO 400:NEXT C:GOTO 2390
2380 TRY=ERRTRY
2390 NEXT TRY:BLOCK=BLOCK+1
2500 IF SH=EOT AND A=ACK THEN 2800
2510 IF SH=CAN OR A<>ACK THEN 2900
2530 GOTO 2300
2800 PUT #MODEM,ACK:? :? "*** SAVING FILE TO DISK ***"
2810 TRAP 2860:CLOSE #MODEM
2820 OPEN #FILE,8,ZERO,FILE$
2830 A=PEEK(BUFF):C=PEEK(BUFF+1):OBJ=0:IF A+C>ZERO AND A+C<510 THEN OBJ=1
2840 POKE 1536,OBJ:ADDR=ADDR+128
2850 C=USR(1610,BUFF,ADDR)
2860 GOTO 2990
2900 ? :? "*** UNABLE TO RECEIVE FILE":A=NAK
2910 PUT #MODEM,ACK
2990 SRFLAG=ZERO:TRAN=ZERO:GOSUB IO:GOTO MENU
3000 TRAN=32:GOSUB IO
3010 SETCOLOR 2,WON,2:BLOCK=ZERO
3020 ? :? "*** SENDING ";FILE$;" ***"
3300 FOR TRY=WON TO ERRTRY
3310 ? :? "*** SENDING SECTOR ";BLOCK+WON;"/";TRY;" ***"
3320 PUT #MODEM,SOH:SUM=ZERO
3330 PUT #MODEM,BLOCK+WON
3340 PUT #MODEM,254-BLOCK
3350 ADDR=BLOCK*128+BUFF:FOR BLK=0 TO 127:C=PEEK(ADDR+BLK):PUT #MODEM,C:? CHR$(C);:SUM=SUM+C:NEXT BLK
3360 SUM=ASC(CHR$(SUM)):PUT #MODEM,SUM
3370 GET #MODEM,A:IF A<>ACK THEN 3390
3380 TRY=ERRTRY
3390 NEXT TRY:BLOCK=BLOCK+1
3500 IF A<>ACK THEN 3900
3510 BYTES=BYTES-128:IF BYTES>ZERO THEN 3300
3800 PUT #MODEM,EOT:PUT #MODEM,ZERO
3810 ? :? "*** TRANSFER COMPLETE ***"
3820 GOTO 3990
3900 ? :? "*** UNABLE TO SEND FILE ***"
3910 PUT #MODEM,CAN
3990 SRFLAG=ZERO:TRAN=ZERO:GOSUB IO:GOTO MENU
4000 ? :CLOSE #MODEM:OPEN #FILE,6,ZERO,"D:*.*"
4010 TRAP 4020:INPUT #FILE;L$:? L$:GOTO 4010
4020 TRAN=ZERO:GOSUB IO:GOTO MENU
5000 IF SRFLAG=82 THEN 2000
5010 IF SRFLAG=83 THEN 3000
5020 ? :? "*** MUST SELECT FIRST! ***"
5030 IF PEEK(CON)<>7 THEN 5030
5040 GOTO TERM
6000 ? :? "*** (Send or Receive)? ";
6010 CLOSE #MODEM:GET #KEY,C:C$=CHR$(C):? C$
6020 IF C$="R" THEN 7000
6030 IF C$="S" THEN 8000
6040 GOSUB IO:GOTO MENU
7000 SRFLAG=ZERO:? :? "*** RECEIVE FILE NAME: ";
7010 INPUT L$:IF L$="" THEN 7090
7020 FILE$=P$:FILE$(LEN(FILE$)+1)=L$
7030 TRAP 7080:OPEN #FILE,4,ZERO,FILE$
7040 ? :? "*** HAVE FILE ";FILE$
7050 ? "*** Type (Y) to ERASE ";FILE$;
7060 GET #KEY,A:? CHR$(A):IF A<>89 THEN 7090
7070 CLOSE #FILE:XIO 36,#FILE,ZERO,ZERO,FILE$:XIO 33,#FILE,ZERO,ZERO,FILE$
7080 SRFLAG=C
7090 TRAP 40000:GOSUB IO:GOTO MENU
8000 ? :? "*** SEND FILE NAME: ";:INPUT L$:IF L$="" THEN 8080
8010 TRAP 8080:FILE$=P$:FILE$(LEN(FILE$)+1)=L$:OPEN #FILE,4,ZERO,FILE$
8020 SRFLAG=C:? "*** LOADING INTO BUFFER ***"
8030 GET #FILE,A:GET #FILE,C:OBJ=0:IF A+C>ZERO AND A+C<510 THEN OBJ=1
8040 CLOSE #FILE:OPEN #FILE,4,ZERO,FILE$
8050 POKE 1536,OBJ
8060 C=USR(1537,BUFF):BYTES=C-BUFF
8070 FOR A=0 TO 127:POKE C+A,0:NEXT A:GOTO 8090
8080 ? "*** FILE NOT FOUND ***":SRFLAG=ZERO
8090 TRAP 40000:GOSUB IO:GOTO MENU
10000 C=FRE(0)-256:DIM BUFF$(C):BUFF=ADR(BUFF$)
10005 ZERO=0:WON=1:SOH=1:EOT=4:ACK=6
10010 BEL=7:BS=8:LF=10:VT=11:CR=13
10020 NAK=21:CAN=24:EOF=26:EOL=155
10030 KEY=1:FILE=2:PTR=3:MODEM=4
10040 DIM C$(1),FILE$(15),L$(130),P$(9):P$="D1:":FILE$=P$
10050 MENU=1000:TERM=1050
10060 ERRTRY=10:CON=53279:IO=14000
10070 OPEN #KEY,4,ZERO,"K:"
10080 GRAPHICS ZERO:? 
10120 XIO 34,#MODEM,192,ZERO,"R1:"
10130 XIO 36,#MODEM,ZERO,ZERO,"R1:"
10180 BUFF$(1)=" ":BUFF$(C)=" "
10190 BUFF$(2,LEN(BUFF$))=BUFF$
11000 ? "    ATARI MODEM FOR CP/M VER-3.0 "
11010 ? " COPYRIGHT(C) 1982 JIM STEINBRECHER"
11020 ? "         37220 TRICIA DRIVE"
11030 ? "       STERLING HTS MI. 48077"
11040 ? :? "BUFFER = ";C;" BYTES ";INT(C/128);" SECTORS":? 
11050 ? "---DO---  then  --------TYPE------"
11060 ? "SELECT R        XMODEM S xxxxx.xxx"
11070 ? "SELECT S        XMODEM R xxxxx.xxx"
11080 ? :? "AFTER CP/M RESPONDS WITH READY"
11090 ? "DEPRESS:  START "
12000 FOR C=1536 TO 1698
12010 READ A:POKE C,A
12020 NEXT C
12030 TRAN=ZERO:GOSUB IO:GOTO MENU
14000 CLOSE #MODEM:CLOSE #PTR:CLOSE #FILE
14010 XIO 38,#MODEM,TRAN,ZERO,"R1:"
14020 OPEN #MODEM,13,ZERO,"R1:"
14030 XIO 40,#MODEM,ZERO,ZERO,"R1:"
14040 POKE 77,0:POKE 766,TRAN/32
14050 RETURN 
15000 DATA 1,104,104,133,213,104,133,212,162,32,169,7,157,66,3,169,0,157,72,3
15010 DATA 157,73,3,32,86,228,192,0,48,40,160,0,145,212,173,0,6,201,1,208
15020 DATA 20,177,212,201,155,208,14,169,13,145,212,230,212,208,2,230,213,169,10,145
15030 DATA 212,230,212,208,2,230,213,24,144,194,96,74,68,83
15040 DATA 104,104,133,204,104,133,203,104,133,206,104,133,205,162,32,169,11,157,66,3
15050 DATA 169,0,157,72,3,157,73,3,160,0,173,0,6,201,1,208,26,177,203,201
15060 DATA 13,208,20,160,1,177,203,201,10,208,12,160,0,230,203,208,2,230,204,169
15070 DATA 155,145,203,160,0,177,203,32,86,228,230,203,208,2,230,204,165,203,197,205
15080 DATA 208,187,165,204,197,206,208,181,96
